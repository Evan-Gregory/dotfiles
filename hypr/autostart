#!/usr/bin/env fish
# --- logging ---
set -l LOG ~/.local/state/hypr/autostart.log
mkdir -p (dirname $LOG)
echo (date --iso-8601=seconds)"  autostart begin" >> $LOG

# --- environment (Wayland apps) ---
set -gx XDG_CURRENT_DESKTOP Hyprland
set -gx XDG_SESSION_TYPE wayland
set -gx MOZ_ENABLE_WAYLAND 1

# --- paths ---
set -l CONFIG $HOME/dotfiles/hypr
set -l SCRIPTS_DIR $CONFIG/scripts

$SCRIPTS_DIR/journal.sh
# --- Waybar ---
if type -q waybar
    if test -x $SCRIPTS_DIR/launch_waybar
        nohup $SCRIPTS_DIR/launch_waybar >> $LOG 2>&1 &
    else
        nohup waybar >> $LOG 2>&1 &
    end
end

# --- Wallpaper (ensure your script exists / is executable) ---
if test -x $SCRIPTS_DIR/wall
    nohup $SCRIPTS_DIR/wall $CONFIG/wallpapers/dark_dwagon.png >> $LOG 2>&1 &
else if type -q swww
    # optional fallback using swww
    swww init >> $LOG 2>&1
    swww img $CONFIG/wallpapers/dark_dwagon.png >> $LOG 2>&1 &
end

# --- RGB effects ---
# if test -x $SCRIPTS_DIR/rgb
#     nohup $SCRIPTS_DIR/rgb >> $LOG 2>&1 &
# end

# --- Tmux Sessions ---
set SESSION "default"
tmux has-session -t $SESSION 2>/dev/null
if test $status -eq 0
    exit 0
end
tmux new-session -d -s $SESSION -n JOURNAL
tmux new-window -t $SESSION:2 -n TERMINAL
tmux new-window -t $SESSION:3 -n DOTS
tmux new-window -t $SESSION:4 -n BTOP


tmux send-keys -t $SESSION:JOURNAL "nvim $JOURNAL_FILE" C-m
tmux send-keys -t $SESSION:DOTS 'cd ~/dotfiles' C-m

# BTOP
tmux send-keys -t $SESSION:BTOP 'btop' C-m

# --- Notifications ---
if type -q dunst
    nohup dunst >> $LOG 2>&1 &
end

# --- PolicyKit agent ---
if test -x /usr/lib/polkit-gnome/polkit-gnome-authentication-agent-1
    nohup /usr/lib/polkit-gnome/polkit-gnome-authentication-agent-1 >> $LOG 2>&1 &
end

# --- DBus env (so apps launched later inherit vars) ---
nohup dbus-update-activation-environment --systemd --all >> $LOG 2>&1 &

# --- Hello toast ---
if type -q notify-send
    notify-send -a aurora "hello "(whoami)
end

echo (date --iso-8601=seconds)"  autostart end" >> $LOG

